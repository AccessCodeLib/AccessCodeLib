Version =20
VersionRequired =20
Checksum =-616906078
Begin Form
    PopUp = NotDefault
    RecordSelectors = NotDefault
    MaxButton = NotDefault
    MinButton = NotDefault
    ControlBox = NotDefault
    AutoCenter = NotDefault
    NavigationButtons = NotDefault
    AllowDeletions = NotDefault
    CloseButton = NotDefault
    DividingLines = NotDefault
    AllowAdditions = NotDefault
    AllowEdits = NotDefault
    DefaultView =0
    ScrollBars =0
    ViewsAllowed =1
    TabularFamily =220
    BorderStyle =0
    PictureAlignment =2
    DatasheetGridlinesBehavior =3
    Cycle =1
    GridY =10
    Width =5670
    DatasheetFontHeight =12
    ItemSuffix =1
    Left =8028
    Top =3588
    Right =16836
    Bottom =8988
    DatasheetGridlinesColor =12632256
    RecSrcDt = Begin
        0x4ae3a063f093e240
    End
    GUID = Begin
        0xa1a24e08036c384d9bcf10a2dd27312a
    End
    Caption ="Information"
    OnOpen ="[Event Procedure]"
    DatasheetFontName ="Arial"
    PrtMip = Begin
        0x6e0400006e0400006e0400006e04000000000000201c0000e010000001000000 ,
        0x010000006801000000000000a10700000100000001000000
    End
    PrtDevMode = Begin
        0x00000000010000006882400018000000dc84400090824000fac29038dc844000 ,
        0x010403069c00400353ef800101000900ea0a6f08640001000f00580202000100 ,
        0x5802030001004c65747465720044db03c082400013894230e044db031a894230 ,
        0xb38ba9f6c8440000000000000000000000000000010000000000000001000000 ,
        0x0200000001000000ffffffff4749533400000000000000000000000044494e55 ,
        0x2200c80024031c00ac13d8c00000000000000000000000000000000000000000 ,
        0x0000000000000000050000000000070000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000100000000000000000000000000000000000000 ,
        0x000000000000000000000000000000000000000000000000c8000000534d544a ,
        0x000000001000b8007b00330045004500330039003100310034002d0033003000 ,
        0x420034002d0034003500610034002d0041003100300039002d00310039004400 ,
        0x3400410034003000460043004300320032007d000000524553444c4c00556e69 ,
        0x726573444c4c00506170657253697a65004c4554544552004f7269656e746174 ,
        0x696f6e00504f525452414954005265736f6c7574696f6e004450493630300043 ,
        0x6f6c6f724d6f6465003234627070000000000000000000000000000000000000 ,
        0x1c0000005634444d0100000000000000000000000000000000000000
    End
    PrtDevNames = Begin
        0x08002c0041000100000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x006e756c3a000000000000000000
    End
    OnTimer ="[Event Procedure]"
    OnClick ="[Event Procedure]"
    OnLoad ="[Event Procedure]"
    FilterOnLoad =0
    AllowLayoutView =0
    DatasheetGridlinesColor12 =12632256
    FitToScreen =255
    PrtDevModeW = Begin
        0x0000400000000000007a40001ea65a76cb1abd80feffffff7a715476ed723530 ,
        0xe07335300a000000cc9f40001e793530220300210016e3050000000024f5ca2f ,
        0x01040306dc00400353ef800101000900ea0a6f08640001000f00580202000100 ,
        0x5802030001004c00650074007400650072000000809f40000016e305fb15e305 ,
        0x00000000e47a40000000e3051e8b3301287a400002000000a47e4000051a9f77 ,
        0x013bd000feff0000000000000000000000000000010000000000000001000000 ,
        0x0200000001000000ffffffff4749533400000000000000000000000044494e55 ,
        0x2200c80024031c00ac13d8c00000000000000000000000000000000000000000 ,
        0x0000000000000000050000000000070000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000100000000000000000000000000000000000000 ,
        0x000000000000000000000000000000000000000000000000c8000000534d544a ,
        0x000000001000b8007b00330045004500330039003100310034002d0033003000 ,
        0x420034002d0034003500610034002d0041003100300039002d00310039004400 ,
        0x3400410034003000460043004300320032007d000000524553444c4c00556e69 ,
        0x726573444c4c00506170657253697a65004c4554544552004f7269656e746174 ,
        0x696f6e00504f525452414954005265736f6c7574696f6e004450493630300043 ,
        0x6f6c6f724d6f6465003234627070000000000000000000000000000000000000 ,
        0x1c0000005634444d0100000000000000000000000000000000000000
    End
    PrtDevNamesW = Begin
        0x040028003d000100000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x0000000000000000000000000000000000000000000000000000000000000000 ,
        0x00000000000000000000000000000000000000000000000000006e0075006c00 ,
        0x3a00000000000000000000000000000000000000
    End
    
    Begin
        Begin Label
            BackStyle =0
            FontName ="Tahoma"
        End
        Begin TextBox
            FELineBreak = NotDefault
            BorderLineStyle =0
            Width =1701
            LabelX =-1701
            FontSize =11
            FontName ="Calibri"
            AsianLineBreak =1
            BorderShade =90.0
            ForeShade =50.0
            GridlineShade =65.0
        End
        Begin Section
            Height =1928
            BackColor =-2147483624
            Name ="Detailbereich"
            OnClick ="[Event Procedure]"
            GUID = Begin
                0x6645a8925546fb40b0b25468410b9419
            End
            Begin
                Begin TextBox
                    Enabled = NotDefault
                    Locked = NotDefault
                    OverlapFlags =85
                    TextAlign =2
                    BackStyle =0
                    IMESentenceMode =3
                    Width =5670
                    Height =1928
                    FontSize =12
                    Name ="txtMessageText"
                    FontName ="Tahoma"
                    OnClick ="[Event Procedure]"
                    GUID = Begin
                        0x261acc75317e32489c72a9120b6de8c9
                    End
                    TextFormat =1
                    HorizontalAnchor =2
                    VerticalAnchor =2

                    LayoutCachedWidth =5670
                    LayoutCachedHeight =1928
                    BorderShade =100.0
                    ForeShade =100.0
                    GridlineShade =100.0
                End
            End
        End
    End
End
CodeBehindForm
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = True
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
'---------------------------------------------------------------------------------------
' Form: frmInfoMessage
'---------------------------------------------------------------------------------------
'/**
' <summary>
' Text in Anwendung einblenden und optional automatisch ausblenden inkl. fadein/fadeout-Feature
' </summary>
' <remarks>
' <code>
' With New Form_frmInfoMessage
'   .Show "Hallo Du!", 3000, 0, 3000, 6000
' End With
' </code>
' </remarks>
'**/
'---------------------------------------------------------------------------------------
'<codelib>
'  <file>form/frmInfoMessage.frm</file>
'  <license>_codelib/license.bas</license>
'</codelib>
'---------------------------------------------------------------------------------------
'
Option Compare Text
Option Explicit

#Const DEBUG_FADEINOUT_TIMER = 0
#Const DEBUG_FADEINOUT_TIMER_STEP = 0

Private m_Self As Form
Private m_FadeOutIsRunning As Boolean
Private m_FadeInIsRunning As Boolean
Private m_FadeOutTime As Long
Private m_FadeInTransparencyTarget As Long
Private m_CloseTimerInterval As Long
Private m_FadeInOutStep As Long

'********************************************************************************
' Deklarationen für Transparente Darstellung

Private m_Transparency As Byte

Private Declare Function GetWindowLong Lib "user32" Alias "GetWindowLongA" (ByVal Hwnd As Long, ByVal nIndex As Long) As Long
Private Declare Function SetWindowLong Lib "user32" Alias "SetWindowLongA" (ByVal Hwnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long
Private Declare Function SetLayeredWindowAttributes Lib "user32" (ByVal Hwnd As Long, ByVal crKey As Long, ByVal bAlpha As Byte, ByVal dwFlags As Long) As Long

'********************************************************************************
' Deklarationen für GetTextHeight

Private Const TWIPSPERINCH = 1440
Private Const LOGPIXELSY = 90
Private Const DT_TOP = &H0
Private Const DT_LEFT = &H0
Private Const DT_CALCRECT = &H400
Private Const DT_WORDBREAK = &H10
Private Const DT_NOCLIP = &H100
Private Const CLIP_LH_ANGLES = 16
Private Const LF_FACESIZE = 32

Private Type RECT
        Left As Long
        Top As Long
        Right As Long
        Bottom As Long
End Type

Private Type LOGFONT
        lfHeight As Long
        lfWidth As Long
        lfEscapement As Long
        lfOrientation As Long
        lfWeight As Long
        lfItalic As Byte
        lfUnderline As Byte
        lfStrikeOut As Byte
        lfCharSet As Byte
        lfOutPrecision As Byte
        lfClipPrecision As Byte
        lfQuality As Byte
        lfPitchAndFamily As Byte
        lfFaceName As String * LF_FACESIZE
End Type

Private Declare Function apiCreateFontIndirect Lib "gdi32" Alias "CreateFontIndirectA" (lpLogFont As LOGFONT) As Long
Private Declare Function apiSelectObject Lib "gdi32" Alias "SelectObject" (ByVal hDC As Long, ByVal hObject As Long) As Long
Private Declare Function apiDeleteObject Lib "gdi32" Alias "DeleteObject" (ByVal hObject As Long) As Long
Private Declare Function apiGetDeviceCaps Lib "gdi32" Alias "GetDeviceCaps" (ByVal hDC As Long, ByVal nIndex As Long) As Long
Private Declare Function apiGetDC Lib "user32" Alias "GetDC" (ByVal Hwnd As Long) As Long
Private Declare Function apiReleaseDC Lib "user32" Alias "ReleaseDC" (ByVal Hwnd As Long, ByVal hDC As Long) As Long
Private Declare Function apiDrawText Lib "user32" Alias "DrawTextA" (ByVal hDC As Long, ByVal lpStr As String, ByVal nCount As Long, lpRect As RECT, ByVal wFormat As Long) As Long
'********************************************************************************

Public Sub Show(Optional ByVal NewInfoText As String = vbNullString, _
                Optional ByVal CloseTimer As Long = 0, _
                Optional ByVal TransparencyFactor As Long = -1, _
                Optional ByVal FadeOutTime As Long = 0, _
                Optional ByVal FadeInTime As Long = 0, _
                Optional ByVal KeepFormReference As Boolean = True)
   
   
   CheckSize
   
   If KeepFormReference Then
      Set m_Self = Me ' .. Formularinstanz halten
   End If
   
   If Len(NewInfoText) > 0 Then
      InfoText = NewInfoText
   End If
   
   If FadeInTime > 0 Then
      Transparency = 100
   ElseIf TransparencyFactor >= 0 Then
      Transparency = TransparencyFactor
   End If

   m_FadeOutTime = FadeOutTime
   m_CloseTimerInterval = CloseTimer
   
   Me.Visible = True
   
   If FadeInTime > 0 Then
      StartFadeIn FadeInTime, TransparencyFactor
   Else
      Me.TimerInterval = m_CloseTimerInterval
   End If
   
End Sub

Private Sub CheckSize()

   If Me.txtMessageText.Width > Me.WindowWidth Then
      Me.txtMessageText.Width = Me.WindowWidth
      Me.InsideWidth = Me.WindowWidth
   End If

End Sub

Public Property Let InfoText(ByVal NewText As String)

   NewText = GetCheckedHtmlRichText(NewText)
   Me.txtMessageText.Value = NewText
   
   If Len(NewText) > 0 Then
      AlignText Me.txtMessageText
   End If
   
End Property

Private Sub AlignText(ByVal ctl As Control)
   
   Dim TextHeight As Long
   TextHeight = GetTextHeight(ctl)
   
   If TextHeight > ctl.Height Then
      Me.InsideHeight = TextHeight + 100
      ctl.TopMargin = 50
      ctl.Height = TextHeight + 100
   Else
      ctl.TopMargin = ((ctl.Height - TextHeight) / 2)
   End If
   
End Sub

Private Function GetCheckedHtmlRichText(ByVal TextToCheck As String) As String
   
   Dim strText As String
   strText = TextToCheck
   
   If Val(Application.SysCmd(acSysCmdAccessVer)) >= 12 Then ' ab Ac2007
   If Me.txtMessageText.Properties("TextFormat") = 1 Then ' 1 = acTextFormatHTMLRichText
      If InStr(1, strText, "<div>", vbTextCompare) = 0 Then ' normaler Text, der im RichTextformat dargestellt werden soll
         strText = "<div>" & Replace(strText, vbNewLine, "<br />") & "</div>"
      End If
   End If
   End If
   
   GetCheckedHtmlRichText = strText
   
End Function


Public Property Let Transparency(ByVal NewTransparency As Byte)
   
   Dim ret As Long
   Dim TransparencyValue As Long
   Dim bAlpha As Byte
   
   m_Transparency = NewTransparency
   If m_Transparency > 100 Then
      m_Transparency = 100
   ElseIf m_Transparency < 0 Then
      m_Transparency = 0
   End If
   
   TransparencyValue = CDbl(m_Transparency) * 2.55
   If TransparencyValue > 255 Then
      TransparencyValue = 255
   ElseIf TransparencyValue < 0 Then
      TransparencyValue = 0
   End If
   
   bAlpha = (255 - TransparencyValue)
   
   ret = GetWindowLong(Me.Hwnd, (-20))
   ret = ret Or &H80000
   SetWindowLong Me.Hwnd, (-20), ret
                               
   SetLayeredWindowAttributes Me.Hwnd, 0, bAlpha, &H2
    
End Property

Public Property Get Transparency() As Byte
   Transparency = m_Transparency
End Property

Public Property Get BackColor() As Long
   BackColor = Me.Section(0).BackColor
End Property

Public Property Let BackColor(ByVal NewValue As Long)
   Me.Section(0).BackColor = NewValue
End Property

Public Property Get ForeColor() As Long
   ForeColor = Me.txtMessageText.ForeColor
End Property

Public Property Let ForeColor(ByVal NewValue As Long)
   Me.txtMessageText.ForeColor = NewValue
End Property

Private Sub Detailbereich_Click()
   CloseForm
End Sub

Private Sub Form_Click()
   CloseForm
End Sub

Private Sub Form_Open(Cancel As Integer)
   '@TODO: Openargs auswerten und Property-Werte daraus setzen
End Sub

Private Sub Form_Load()
'  AlignText Me.txtMessageText
End Sub

Private Sub txtMessageText_Click()
   CloseForm
End Sub

Private Sub Form_Timer()
   
   If m_FadeOutIsRunning Then
      RunFadeOutStep
   ElseIf m_FadeInIsRunning Then
      RunFadeInStep
   Else
      Me.TimerInterval = 0
      If m_FadeOutTime > 0 Then
         StartFadeOut
      Else
#If DEBUG_FADEINOUT_TIMER Then
   Debug.Print ObjPtr(Me), "CloseForm", Timer, Now()
#End If
         CloseForm
      End If
   End If
   
End Sub

Private Sub StartFadeOut()

   Me.TimerInterval = 0

   m_FadeOutIsRunning = True

   If Me.Transparency >= 100 Then
      m_FadeInOutStep = 1
      Me.TimerInterval = 1
      Exit Sub
   End If
   
   
   Me.TimerInterval = CalcFadeInFadeOutTimerInverval(m_FadeOutTime, (100 - Me.Transparency))

#If DEBUG_FADEINOUT_TIMER Then
   Debug.Print ObjPtr(Me), "StartFadeOut", Me.TimerInterval, Timer, Now()
#End If
      
End Sub

Private Function CalcFadeInFadeOutTimerInverval(ByVal FadeInOutTime As Long, ByVal TransparenceDiff As Long) As Long

   Dim lngTimerInveral As Long

   m_FadeInOutStep = Int(CDbl(TransparenceDiff) / (CDbl(FadeInOutTime) / 100#))
   If m_FadeInOutStep = 0 Then
      m_FadeInOutStep = 1
   End If
   
   lngTimerInveral = Int(CDbl(FadeInOutTime) / (CDbl(TransparenceDiff) / CDbl(m_FadeInOutStep)))
   If lngTimerInveral = 0 Then
      lngTimerInveral = 1
   End If
   
#If DEBUG_FADEINOUT_TIMER Then
   Debug.Print ObjPtr(Me), "CalcFadeInFadeOutTimerInverval", FadeInOutTime, TransparenceDiff, lngTimerInveral, m_FadeInOutStep
#End If
   
   CalcFadeInFadeOutTimerInverval = lngTimerInveral

End Function


Private Sub RunFadeOutStep()

   Dim NewTransparency As Long

   NewTransparency = Me.Transparency + m_FadeInOutStep
   If NewTransparency > 100 Then
      NewTransparency = 100
   End If
   
   Me.Transparency = NewTransparency

#If DEBUG_FADEINOUT_TIMER_STEP Then
   Debug.Print ObjPtr(Me), "RunFadeOutStep", m_FadeInOutStep, Timer, Now()
#End If
   
   If Me.Transparency >= 99 Then
#If DEBUG_FADEINOUT_TIMER Then
      Debug.Print ObjPtr(Me), "CloseFadeOutStep", Timer, Now()
#End If
      CloseFadeOut
   End If

End Sub

Private Sub CloseFadeOut()
   Me.TimerInterval = 0
   m_FadeOutIsRunning = False
   CloseForm
End Sub

Private Sub CloseForm()
   DoCmd.Close
   If Not (m_Self Is Nothing) Then
      Set m_Self = Nothing
   End If
End Sub

Private Sub StartFadeIn(ByVal FadeInTime As Long, ByVal TargetTransparencyFactor As Long)
   
   If TargetTransparencyFactor >= 0 Then
      m_FadeInTransparencyTarget = TargetTransparencyFactor
   Else
      m_FadeInTransparencyTarget = 0
   End If
   
   m_FadeInIsRunning = True
   
   If m_FadeInTransparencyTarget >= 100 Then
      m_FadeInOutStep = 1
      Me.TimerInterval = 1
      Stop
      Exit Sub
   End If
   
   Me.TimerInterval = CalcFadeInFadeOutTimerInverval(FadeInTime, 100 - m_FadeInTransparencyTarget)

#If DEBUG_FADEINOUT_TIMER Then
   Debug.Print ObjPtr(Me), "StartFadeIn", Me.TimerInterval, Timer, Now()
#End If

End Sub

Private Sub RunFadeInStep()
   
   Dim NewTransparency As Long
   
   NewTransparency = Me.Transparency - m_FadeInOutStep
   
   If NewTransparency <= m_FadeInTransparencyTarget Then
      CloseFadeIn
      Exit Sub
   End If
   
   Me.Transparency = NewTransparency
   
#If DEBUG_FADEINOUT_TIMER_STEP Then
   Debug.Print ObjPtr(Me), "RunFadeInStep", m_FadeInOutStep, Timer, Now()
#End If
   
End Sub

Private Sub CloseFadeIn()
   
   Me.TimerInterval = 0
   m_FadeInIsRunning = False
   
#If DEBUG_FADEINOUT_TIMER Then
   Debug.Print ObjPtr(Me), "CloseFadeIn", Me.TimerInterval, Timer, Now()
#End If
      
   Me.Transparency = m_FadeInTransparencyTarget
   
   Me.TimerInterval = m_CloseTimerInterval
   
#If DEBUG_FADEINOUT_TIMER Then
   If m_CloseTimerInterval > 0 Then
      Debug.Print ObjPtr(Me), "StartCloseTimer", Me.TimerInterval, Timer, Now()
   End If
#End If
   
End Sub

Private Function GetTextHeight(ByVal ctl As Control) As Long
' = angepasster Code der Funktion fTextWidthOrHeight von Stephen Lebans
'
' Original-Code: http://www.lebans.com/verticaljustification.htm
'
' Änderungen:
'     * gekürzt, da nur Höhe benötigt wird
'     * Funktionalität für Berichte entfernt
'     * Text wird aus Steuerelement gelesen
'     * Nicht mehr benötigte Code-Blöcke entfernt
'
'
' Prozedur-Kopf von "fTextWidthOrHeight":
'*********************************************************
 'Name                   FUNCTION() fTextWidthOrHeight
 '
 'Purpose:               Returns the Height or Width needed to
 '                       display the contents of the Control passed
 '                       to this function. This function
 '                       uses the Control's font attributes to build
 '                       a Font for the required calculations.
 '
 '                       This function replaces the Report object's TextHeight
 '                       and TextWidth methods which only work for a single line of text.
 '                       This function works with multiple lines of text and
 '                       also with both Forms and Reports.
 '
 'Version:               4.1
 '
 'Calls:                 Text API stuff. DrawText performs the actual
 '                       calculation to determine Control Height.
 '
 'Returns:               Height or width of Control in TWIPS required
 '                       to display current contents.
 '
 'Created by:            Stephen Lebans
 '
 'Credits:               If you want some...take some.
 '
 'Date:                  May 22, 2001
 '
 'Time:                  10:10:10pm
 '
 'Feedback:              Stephen@lebans.com
 '
 'My Web Page:           www.lebans.com
 '
 'Copyright:             Lebans Holdings Ltd.
 '                       Please feel free to use this code
 '                       without restriction in any application you develop.
 '                       This code may not be resold by itself or as
 '                       part of a collection.
 '
 'What's Missing:        Let me know!
 '
 '
 '
 'Bugs:
 'None at this point.
 '
 'Enjoy
 'Stephen Lebans
 
'*********************************************************
 
   Dim strText As String
   
   ' Structure for DrawText calc
   Dim sRect As RECT
   
   ' Reports Device Context
   Dim hDC As Long
   
   ' Holds the current screen resolution
   Dim lngDPI As Long
   
   Dim newfont As Long
   ' Handle to our Font Object we created.
   ' We must destroy it before exiting main function
   
   Dim oldfont As Long
   ' Device Context's Font we must Select back into the DC
   ' before we exit this function.
 
   ' Temporary holder for returns from API calls
   Dim lngRet As Long
   
   ' Logfont struct
   Dim myfont As LOGFONT
   
   ' LineSpacing Amount
   Dim lngLineSpacing As Long
   
   ' Ttemp var
   Dim numLines As Long
   
   ' Temp string var for current printer name
   Dim strName As String
   
   ' Temp vars
   Dim sngTemp1 As Single
   Dim sngTemp2 As Single
 
   hDC = apiGetDC(0&)

   Select Case ctl.ControlType
       Case acTextBox
         strText = Nz(ctl.Value, vbNullString)
         If Val(Application.SysCmd(acSysCmdAccessVer)) >= 12 Then ' ab Ac2007
         If ctl.Properties("TextFormat") = 1 Then ' 1 = acTextFormatHTMLRichText
            strText = GetClearedTextFromHtmlRichTextBox(ctl)
         End If
         End If
       
       Case acLabel, acCommandButton
         strText = Nz(ctl.Caption, vbNullString)
       
       Case acListBox
         strText = Nz(ctl.ItemData(0), vbNullString)
       
       Case Else
         Err.Raise vbObjectError, "GetTextHeight", "ControlType not supported"
   End Select
   
   ' Get current device resolution
   lngDPI = apiGetDeviceCaps(hDC, LOGPIXELSY)

   ' We use a negative value to signify
   ' to the CreateFont function that we want a Glyph
   ' outline of this size not a bounding box.
   ' Copy font stuff from Text Control's property sheet
   With ctl
      myfont.lfClipPrecision = CLIP_LH_ANGLES
      myfont.lfOutPrecision = 0 ' sl APRIL 08/2002OUT_TT_ONLY_PRECIS
      myfont.lfEscapement = 0
      myfont.lfFaceName = .FontName & Chr$(0)
      myfont.lfWeight = .FontWeight
      myfont.lfItalic = .FontItalic
      myfont.lfUnderline = .FontUnderline
      'Must be a negative figure for height or system will return
      'closest match on character cell not glyph
      myfont.lfHeight = (.FontSize / 72) * -lngDPI
      ' Create our temp font
      newfont = apiCreateFontIndirect(myfont)
   End With
   
   If newfont = 0 Then
      Err.Raise vbObjectError + 256, "GetTextHeight", "Cannot Create Font"
   End If

   ' Select the new font into our DC.
   oldfont = apiSelectObject(hDC, newfont)
   ' Use DrawText to Calculate height of Rectangle required to hold
   ' the current contents of the Control passed to this function.
   With sRect
   
      .Left = 0
      .Top = 0
      .Bottom = 0
      .Right = (ctl.Width / (TWIPSPERINCH / lngDPI)) - 10
      
      ' Calculate our bounding box based on the controls current width
      lngRet = apiDrawText(hDC, strText, -1, sRect, DT_CALCRECT Or DT_TOP Or DT_LEFT Or DT_WORDBREAK Or DT_NOCLIP)

      ' Cleanup
      lngRet = apiSelectObject(hDC, oldfont)
      ' Delete the Font we created
      apiDeleteObject (newfont)
      
      lngRet = apiReleaseDC(0&, hDC)
      
      GetTextHeight = .Bottom * (TWIPSPERINCH / lngDPI)
      
   End With

End Function

Private Function GetClearedTextFromHtmlRichTextBox(ByVal tb As TextBox) As String
   
   Dim strTemp As String
   
   strTemp = Nz(tb.Value, vbNullString)

   If InStr(1, strTemp, "<div>", vbTextCompare) > 0 Then ' Text wird Access-Html-Richtext entsprechen => Zeilenumbruch = div-Tag
      'strTemp = Application.PlainText(strTemp) ' PlainText-Funktion ist erst ab 2007 verfügbar => CallByName, damit auch mit 2003 kompiliert werden kann
      strTemp = VBA.CallByName(Application, "PlainText", VbMethod, strTemp)
   Else 'use Replace - => Zeilenumbruch = vbNewline oder <br>-Tag
      strTemp = Replace(strTemp, "<br />", vbNewLine)
      strTemp = Replace(strTemp, "<br/>", vbNewLine)
      strTemp = Replace(strTemp, "<br>", vbNewLine)
   End If
   
   GetClearedTextFromHtmlRichTextBox = strTemp

End Function